# SimbodyConfig.cmake

# Adapted from FindSimbody.cmake
#
# This should define the following:
#   Simbody_FOUND - Whether search for Simbody libraries and headers succeeded.
#   Simbody_ROOT_DIR - the installation directory; all the pieces must be
#                      found together
#   Simbody_INCLUDE_DIR - location of Simbody.h
#   Simbody_LIB_DIR     - location of libSimTKsimbody.{a,so,dylib} 
#                         or SimTKsimbody.lib
#   Simbody_BIN_DIR     - location of VisualizerGUI and .dll's on Windows
#   Simbody_LIBRARIES   - suitable for target_link_libraries(); includes
#                         both optimized and debug libraries if both are
#                         available
#   Simbody_STATIC_LIBRARIES - suitable for target_link_libraries(); includes
#                              both optimized and debug static libraries if
#                              both are available
#
# The following variables can be used in your own project so that your
# project's Doxygen documentation can link with Simbody's. These variables are
# only defined if Doxygen documentation is installed.
#   Simbody_DOXYGEN_DIR     - Directory containing Doxygen API documentation.
#   Simbody_DOXYGEN_TAGFILE - Path to SimbodyDoxygenTagFile.
#
# For example, if you're configuring your Doxyfile using CMake's
# configure_file, your Doxyfile.in file (to be configured) could contain
# (without the backslashes):
#
#   TAGFILES = "\@Simbody_DOXYGEN_TAGFILE\@=\@Simbody_DOXYGEN_DIR\@"

if (@PKG_NAME@_CONFIG_INCLUDED)
  return()
endif()
set(@PKG_NAME@_CONFIG_INCLUDED TRUE)

# Watch out for spaces in pathnames -- must quote.

# Compute paths
get_filename_component(SIMBODY_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

set(Simbody_ROOT_DIR
            "@CMAKE_INSTALL_PREFIX@")

set(Simbody_INCLUDE_DIR
            "@CMAKE_INSTALL_PREFIX@/@SIMBODY_INCLUDE_INSTALL_DIR@")

set(Simbody_LIB_DIR
            "@CMAKE_INSTALL_PREFIX@/@CMAKE_INSTALL_LIBDIR@") #shouldn't be needed

set(Simbody_BIN_DIR
            "@SIMBODY_VISUALIZER_INSTALL_DIR@")

set(Simbody_CFLAGS
            -I"@CMAKE_INSTALL_PREFIX@/@SIMBODY_INCLUDE_INSTALL_DIR@")

set(Simbody_LDFLAGS
            -L"@CMAKE_INSTALL_PREFIX@/@CMAKE_INSTALL_LIBDIR@")

if (NOT "@SIMBODY_DOXYGEN_TAGFILE_NAME@" STREQUAL "")
    # Must check tagfile variable, since the doxygen install dir is created
    # even if Doxygen documentation is not install.
    set(temp_doxygen_dir
        "@CMAKE_INSTALL_PREFIX@/@SIMBODY_INSTALL_DOXYGENDIR@")
    set(temp_tagfile_path
        "${temp_doxygen_dir}/@SIMBODY_DOXYGEN_TAGFILE_NAME@")
    if (EXISTS "${temp_tagfile_path}")
        set(@PKG_NAME@_DOXYGEN_DIR "${temp_doxygen_dir}")
        set(@PKG_NAME@_DOXYGEN_TAGFILE "${temp_tagfile_path}")
    endif()
    unset(temp_doxygen_dir)
    unset(temp_tagfile_path)
endif()


# Our library dependencies (contains definitions for IMPORTED targets)
if(NOT TARGET @SimTKCOMMON_SHARED_LIBRARY@ AND NOT Simbody_BINARY_DIR)
  include("${SIMBODY_CMAKE_DIR}/SimbodyTargets.cmake")
endif()

# These are IMPORTED targets created by SimbodyTargets.cmake
set(Simbody_LIBRARIES @SimTKCOMMON_SHARED_LIBRARY@ @SimTKMATH_SHARED_LIBRARY@ @SimTKSIMBODY_SHARED_LIBRARY@)
set(Simbody_STATIC_LIBRARIES @SimTKCOMMON_STATIC_LIBRARY@ @SimTKMATH_STATIC_LIBRARY@ @SimTKSIMBODY_STATIC_LIBRARY@)

mark_as_advanced(Simbody_LIBRARIES Simbody_STATIC_LIBRARIES)
